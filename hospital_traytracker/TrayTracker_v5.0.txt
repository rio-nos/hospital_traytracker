Private Sub Worksheet_Change(ByVal Target As Range)
    'Define regex to match cart # or initials when delivering without a cart
    'In order for there to be a time stamp,
    'cell value must be in the form of ## or @@@@@ where @ = a letter
    Dim regex As Object
    Dim allMatches As Object
    Set regex = CreateObject("VBScript.RegExp")
    regex.ignorecase = True
    regex.Pattern = "(^\d{1,2}$)|(^[a-z]{1,5}$)"
    'Only run this code if we are selecting/highlighting one cell
    If Target.Rows.Count = 1 And Target.Columns.Count = 1 Then
        'Target = Cart # Column --> Time stamp and fill cells below until current row contains empty cell in room # column
        If Target.Column = 5 Then
            ActiveSheet.Unprotect
            Application.EnableEvents = False
            If (Not (IsEmpty(Cells(Target.Row, 3).Value) And IsEmpty(Target.Value))) Then
                Set allMatches = regex.Execute(Target.Value)
                If allMatches.Count > 0 Then
                    Dim CurrentC As Integer
                    CurrentC = Target.Row
                    Cells(CurrentC, 5).Value = Target.Value
                    Cells(CurrentC, 6).Value = Time
                    CurrentC = CurrentC + 1
                    Do While (Not IsEmpty(Cells(CurrentC, 3).Value)) And _
                    (IsEmpty(Cells(CurrentC, Target.Column).Value) Or InStr(Cells(CurrentC, Target.Column).Value, ".") > 0)
                        Cells(CurrentC, 5).Value = "."
                        Cells(CurrentC, 6).Value = Time
                        CurrentC = CurrentC + 1
                    Loop
                End If
            End If
            Application.EnableEvents = True
            ActiveSheet.Protect
        End If
        'Initials column
        If Target.Column = 8 Then
            ActiveSheet.Unprotect
            Application.EnableEvents = False
            If Not (IsEmpty(Cells(Target.Row, 5).Value) And IsEmpty(Target.Value)) Then
                Set allMatches = regex.Execute(Target.Value)
                If allMatches.Count > 0 Then
                    Dim CurrentI As Integer
                    CurrentI = Target.Row
                    Cells(CurrentI, 8).Value = Target.Value
                    Cells(CurrentI, 9).Value = Time
                    CurrentI = CurrentI + 1
                    Do While (Not IsEmpty(Cells(CurrentI, 5).Value)) And _
                    (IsEmpty(Cells(CurrentI, Target.Column).Value) Or InStr(Cells(CurrentI, 5).Value, ".") > 0)
                        Cells(CurrentI, 8).Value = Target.Value
                        Cells(CurrentI, 9).Value = Time
                        CurrentI = CurrentI + 1
                    Loop
                End If
            End If
            Application.EnableEvents = True
            ActiveSheet.Protect
        End If
        'For Ticket Print Time and Tray Arrival Time
        If Target.Column = 4 Or Target.Column = 10 Then
            If Target.Value Like "####" Then
                    Application.EnableEvents = False
                    Target.Value = Left(Target.Value, 2) & ":" & Right(Target.Value, 2)
                    Application.EnableEvents = True
            ElseIf Target.Value Like "###" Then
                    Application.EnableEvents = False
                    Target.Value = Left(Target.Value, 1) & ":" & Right(Target.Value, 2)
                    Application.EnableEvents = True
            End If
        End If
    End If
End Sub
